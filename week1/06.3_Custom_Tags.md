为了更好地在前端展示代码，另一个选项是使用自定义标签。自定义标签在它们各自的目录下，并且使用"cf_"这个前缀。在一个cfml页面上放置一个自定义标签将执行标签中包含的代码，就好像它已经包含在页面本身上一样。为了开始，我们先创建一个新的`.cfm`文件来存放我们的自定义标签。我们叫它`greeting.cfm`。首先我们来确认一下标签的`executionMode`。如果`executionMode`是"start"，这意味着起始标签刚刚被执行。如果`executionMode`是"end"，这意味着这个标签刚刚被关上。这个过程可以通过编写这样一个if语句来看到：
```cfml
<cfif thisTag.executionMode EQ "start">

    <!--- 这个标签刚刚被打开时会被执行的代码 --->

<cfelse>

    <!--- 这个标签刚刚被关上时会被执行的代码 --->

</cfif>
```

因为我们的例子相对比较简单，我们将会侧重于讲解在`executionMode`为"start"的时候会被执行的代码。

像以前一样，我们将希望构建用户的全名，这意味着我们可能希望将它们传递到标签中。将属性设置在标签上，就像任何其它ColdFusion标签实现这一点一样；这些属性然后被存储在属性范围中。对它们的访问是通过使用`attributes.ourVariableName`完成的，如下例所示:

```cfml
<cfif thisTag.executionMode EQ "start">

    <cfset fullName = attributes.firstName & " " & attributes.lastName>

    <cfoutput>
          Hello, #fullName#
    </cfoutput>

</cfif>
```

一个greeting再次被构建，并且我们也准备好在要显示的页面上使用这个标签。回到displayPage.cfm，这样写：

```cfml
<cf_greeting firstName="Matt" lastName="Gifford">
```

这会在浏览器输出"Hello, Matt Gifford"。

你还可以使用cfimport来将多个标签引入你的页面，并把它们放在各自的命名空间中。这些标签仅有两个参数：

- **prefix**: 这个参数是开发者用来引用这个标签的前缀。这个参数会替代标准的"cf_"。
- **taglib**: 这个是标签库相对于网站根目录的路径。

现在，我们可以用import标签来引入几个标签，然后给它们一个更具描述性的前缀。

```cfml
<cfimport prefix="display" taglib="/path/to/tags">

<display:greeting firstName="Matt" lastName="Gifford">
```

现在，很容易就能看出greeting标签属于一个叫display的包。这给了我们一个关于这个标签是做什么的线索。它显示greeting。
